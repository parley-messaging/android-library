apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'org.jetbrains.kotlin.android'

group = 'com.github.parley-networking'
version = '3.10.0'

android {
    namespace 'nu.parleynetwork.android'
    compileSdk 34

    defaultConfig {
        minSdk 21
        targetSdk 34
        versionCode 1
        versionName version

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        consumerProguardFiles 'proguard-rules.pro'
    }
    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = group
                artifactId = 'android-library'
                version = android.defaultConfig.versionName
            }
        }
    }
}

dependencies {
    implementation project(':parley')

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"

    // Glide
    implementation "com.github.bumptech.glide:glide:4.15.1"
    implementation 'androidx.core:core-ktx:1.13.1'
    annotationProcessor "com.github.bumptech.glide:compiler:4.15.1"

    // TrustKit
    implementation 'com.datatheorem.android.trustkit:trustkit:1.1.5'

    testImplementation 'junit:junit:4.13.2'
}